\chapter{Lawvere Theory Formalisation}
This chapter describes the formalisation of the type of Lawvere Theories and the
category of Lawvere Theories, including explanation of the majority of the Coq
code that makes up the formalisation\footnote{Full code available at
\url{https://github.com/Mjiig/LawvereTheoryCsystems/blob/master/Lawvere.v}}.

\section{The Category $F$}
\subsection{Category Structure}
To define the category $F$ we first define $stn$ as
\begin{lstlisting}
Definition stn (n : nat) : UU := total2 ( fun x : nat => n > x).
\end{lstlisting}
where \lstinline|total2| is used to define a dependent sum, such that an element
of \lstinline|stn(n)| consists of a natural number \lstinline|x| and a proof
that \lstinline|n| is greater than \lstinline|x|. The type \lstinline|UU| is the
universe of types we are working in, and informally can be read as meaning that
we are defining a type. Coq itself has no notion of a set so we use types in
their place. It is worth noting that it is not immediately obvious that
\lstinline|stn(n)| has exactly $n$ members, since there could conceivably be
multiple proofs of $n>x$ for the same value of $x$, however this is not the case
because for any \lstinline|a| and \lstinline|b| \textit{UniMath}'s built in type
\lstinline|a>b| has h-level 1.

Given the definition of \lstinline|stn| we formalise $F_{mor}(n,m)$ as
\begin{lstlisting}
Definition F_mor (n m : nat) := stn(n) -> stn(m).
\end{lstlisting}
which is just the type of functions from the type \lstinline|stn(n)| to the type
\lstinline|stn(m)|. These two components give the structure of the category $F$,
which we can encode in \textit{UniMath}
\begin{lstlisting}
Definition F_ob_mor := precategory_ob_mor_pair nat F_mor.
\end{lstlisting}

This is not a category or precategory object, as we need to give the identities
and composition explicitly,
\begin{lstlisting}
Definition F_identity (n : nat) : (F_mor n n) := (fun x => x).

Definition F_comp (a b c : nat) (f : F_mor a b) (g : F_mor b c):=
    fun (x : stn a) => g (f x).
\end{lstlisting}
This code gives the identity morphism and morphism composition for $F$, which is
just the identity function and function composition though with more explicit
types.

This is sufficient to define the structure of $F$,
\begin{lstlisting}
Definition F_data := 
  precategory_data_pair F_ob_mor F_identity F_comp.
\end{lstlisting}
but it remains to prove that $F$ is a category.

\subsection{Category Axioms}
Obviously all three category axioms on $F$ are trivial, and this is also true
for their proofs in Coq,
\begin{lstlisting}
Definition F_left_comp (n m : nat) (f : F_mor n m) : 
  (F_identity n) · f = f.
Proof.
  apply idpath.
Defined.

Definition F_right_comp (n m : nat) (f : F_mor n m) : 
  f · (F_identity m) = f.
Proof.
  apply idpath.
Defined.

Definition F_assoc_comp 
  (k l m n : nat)
  (f : F_mor k l) (g : F_mor l m) (h : F_mor m n) :
  (f · g) · h = f · (g · h).
Proof.
  apply idpath.
Defined.
\end{lstlisting}

Since Coq can tell that \lstinline|F_identity m| is just the identity function it
will apply beta reduction to expressions containing it, which immediately
eliminates it. Similarly, just expanding the definition of composition leads to
both sides of the associativity axiom being identical. By combining these proofs
we get a proof that \lstinline|F_data| is the structure of a precategory, and can
use that proof to construct the precategory itself.

\begin{lstlisting}
Definition F_is_precategory := 
  mk_is_precategory (C:=F_data) 
    F_left_comp F_right_comp F_assoc_comp.

Definition F_pre : precategory := 
  mk_precategory F_data F_is_precategory.
\end{lstlisting}

\subsection{Homomorphism Sets}
Although not strictly necessary, we would like to demonstrate that $F$ is in
fact a category rather than just a precategory. This amounts to proving that the
type \lstinline|F_mor n m| is a set,

\begin{lstlisting}
Definition stn_isaset (n : nat) : isaset (stn n).
Proof.
  apply (isofhleveltotal2 2).
  - exact isasetnat.
  - intros.
    assert (isofhlevel 1 (n>x)).
    + apply natgth.
    + apply hlevelntosn; assumption.
Defined.

Definition F_has_homsets (n m : nat) : isaset (F_mor n m).
Proof.
    change isaset with (isofhlevel 2).
    apply impred.
    intros.
    apply stn_isaset.
Defined.
\end{lstlisting}

We first prove that for any \lstinline|n|, \lstinline|stn(n)| is a set. The tactic
\lstinline|isofhleveltotal2| reduces this to proving that \lstinline|nat| and \lstinline|n>x|
are both of h-level 2 (for any \lstinline|x|). That \lstinline|nat| is a set is a theorem
proven in \textit{UniMath}. As already mentioned \lstinline|n>x| is of h-level 1, and
we use \lstinline|hlevelntosn| to show that it is therefore also of h-level 2.

We can now use \lstinline|impred| to show that \lstinline|F_mor n m| is of h-level 2.
Using \lstinline|impred| reduces the proof that functions from anything into
\lstinline|stn(m)| are of h-level 2 to showing that \lstinline|stn(m)| is of h-level 2,
which is established by \lstinline|stn_isaset|.

Finally given these proofs we can construct a \textit{UniMath} category for $F$
as
\begin{lstlisting}
Definition F : category := category_pair F_pre F_has_homsets.
\end{lstlisting}

\subsection{Coproduct Structure}
We also formalise the coproduct structures on $F$ so that we can specify that
they are preserved. To do this we first define the inclusions $\iota_1^{n,m}$
and $\iota_2^{n,m}$, for which we need a few lemmas.

\begin{lstlisting}
Lemma m_lt_sn {n m} (P : n > m) : S n > m.
Proof.
  apply (istransnatgth (S n) (S m) m).
  + cbn.
    assumption.
  + exact (natgthsnn m).
Defined.

Lemma x_lt_nm {n m x : nat} (p : n > x) : (n + m) > x.
Proof.
  induction m.
  + rewrite natplusr0.
    assumption.
  + rewrite natplusnsm.
  exact (m_lt_sn IHm).
Defined.

Lemma xn_lt_nm {n m x : nat} (p : m > x) : (n + m) > (n + x).
Proof.
  induction n.
  + rewrite !natplusl0.
    assumption.
  + cbn.
    cbn in IHn.
    assumption.
Defined.
\end{lstlisting}

These essentially just prove that if $x\in stn(n)$ then $\iota_1^{n,m}(x) \in
stn(n+m)$ and if $x\in stn(m)$ then $\iota_2^{n,m}(x)\in stn(n+m)$. Given the
definitions of the inclusions these results are obvious, but proofs of them are
needed in the formalisation of the inclusions. The proofs have been formalised
so as to make best use of the existing theorems about ordering relations on the
natural numbers given in \textit{UniMath}.

From these we can define the inclusions themselves
\begin{lstlisting}
Definition F_inc1 (n m : nat) : F_mor n (n+m) := 
  (fun x => tpair _ (pr1 x) (x_lt_nm (pr2 x))).

Definition F_inc2 (n m : nat) : F_mor m (n+m) := 
  (fun x => tpair _ (n + (pr1 x)) (xn_lt_nm (pr2 x))).
\end{lstlisting}
which have the expected behaviour on the numeric part of the input pair, and use
the above proven lemmas to get the necessary proof that it is in
\lstinline|stn(n+m)|.

We are also required to give, for any object $X$ and morphisms $f: stn(n)\to X$
and $g: stn(m)\to X$ the unique arrow from $stn(n+m)$ to $X$ such that the
standard coproduct diagram commutes. To do so we first establish a lemma of type
\begin{lstlisting}
Lemma r (n m x : nat) (p : x >= n) (q : n+m > x) : m > x - n.
\end{lstlisting}
the proof of which has been omitted for brevity. Using this we define the
coproduct arrow as
\begin{lstlisting}
Definition F_coprod_rect 
  (k n m : nat) (f : F_mor n k) (g : F_mor m k) : 
  F_mor (n+m) k :=
    fun (x : stn (n+m)) => 
      coprod_rect 
        (fun _ => stn k)
        (fun p =>  f (tpair _ (pr1 x) p))
        (fun p =>  
          g (tpair _ ((pr1 x) - n) (r n m (pr1 x) p (pr2 x))))
        (natlthorgeh (pr1 x) n).
\end{lstlisting}
which uses an existing theorem to establish either \lstinline|n>x| or
\lstinline|x>=n|. In the first case the function uses the proof of
\lstinline|n>x| to apply \lstinline|f|. In the second case the function uses
\lstinline|r| to establish \lstinline|m>x-n| and from that can apply
\lstinline|g|.

Given this function it is possible to construct a term of type
\begin{lstlisting}
Definition F_coprods (n m : nat) : 
  isBinCoproduct F n m (n + m) (F_inc1 n m) (F_inc2 n m).
\end{lstlisting}
the Coq proof of which is long but trivial. This establishes that the inclusions
give a coproduct structure in $F$.

\section{Lawvere Theory Axioms}
The structure of a Lawvere Theory is simply encoded as
\begin{lstlisting}
Definition LT_data := total2 (fun (T:category) => functor F T).
\end{lstlisting}
with the following projections to extract $T$ and $L$
\begin{lstlisting}
Definition LT_T (LT : LT_data) : category := pr1 LT.
Definition LT_L (LT : LT_data) : functor F (LT_T LT) := pr2 LT.
\end{lstlisting}
with respect to which we define the Lawvere Theory axioms.

The first Lawvere Theory axiom, that $L$ be a bijection on objects is formalised
as
\begin{lstlisting}
Definition LT_bij_on_objects (LT : LT_data) := 
  isweq (functor_on_objects (LT_L LT)).
\end{lstlisting}
where \lstinline|(functor_on_objects (LT_L LT))| just gives the function that
describes the behaviour of $L$ on objects and \lstinline|isweq| states that the
function is an equivalence, or a bijection. In fact \lstinline|functor_on_objects|
here could be omitted as this coercion is automatic, but it has been kept for
clarity.

From this bijection constraint we are able to extract an inverse function of $L$
on objects and the expected rewriting rules for cancelling \lstinline|L| and its
inverse.
\begin{lstlisting}
Definition LT_L_inv (lt : LT) : (LT_T lt) -> nat :=
  invmap (LT_L_weq lt).

Lemma LT_L_cancel (lt : LT) (x : nat) : 
  LT_L_inv lt (LT_L lt x) = x.
Proof.
  unfold LT_L_inv.
  apply homotinvweqweq.
Defined.
  
Lemma LT_L_cancel2 (lt : LT) (x : LT_T lt) : 
  LT_L lt (LT_L_inv lt x) = x.
Proof.
  unfold LT_L_inv.
  change (functor_on_objects (LT_L lt)) 
    with (pr1weq (LT_L_weq lt)).
  apply homotweqinvweq.
Defined.
\end{lstlisting}

The second Lawvere Theory axiom, that $L(0)$ be initial, is formalised using an
already defined concept of initial objects from \textit{UniMath},
\begin{lstlisting}
Definition LT_L0_initial (LT : LT_data) := 
  isInitial (LT_T LT) (LT_L LT 0).
\end{lstlisting}
where \lstinline|isInitial C x| is defined in the obvious way by
\textit{UniMath}, as a function from objects of \lstinline|C| to morphisms out
of \lstinline|x| along with proofs that they are unique.

Finally we formalise the requirement that $L$ preserves coproducts, using the
previously formalised inclusions,
\begin{lstlisting}
Definition LT_has_coprods (LT : LT_data) := 
  ∏ n m : nat, 
    isBinCoproduct 
      (LT_T LT)
      (LT_L LT n)
      (LT_L LT m)
      (LT_L LT (n+m))
      (#(LT_L LT) (F_inc1 n m)) 
      (#(LT_L LT) (F_inc2 n m)).
\end{lstlisting}
where \lstinline|isBinCoproduct| is similarly defined by \textit{UniMath} in the
expected way and \lstinline|#| gives the function describing the behaviour of a
functor on morphisms.

We can combine all these requirements into a single structure as
\begin{lstlisting}
Definition is_LT (LT : LT_data)
  := (LT_bij_on_objects LT) × (LT_L0_initial LT) × (LT_has_coprods LT).
\end{lstlisting}
from which we define a Lawvere Theory to be
\begin{lstlisting}
Definition LT := total2(fun LT_data : LT_data => is_LT LT_data).
\end{lstlisting}

\section{The category of Lawvere Theories}
\subsection{Morphisms Between Lawvere Theories}
As before, we first define the basic structure of the morphisms, then the
constraints upon them and combine the two as a dependent sum. The structure of a
Lawvere Theory morphism is very simple, since functors are already formalised by
\textit{UniMath}.
\begin{lstlisting}
Definition LT_mor_data (A B : LT) := functor (LT_T A) (LT_T B).
\end{lstlisting}

The single constraint on Lawvere Theory morphisms is comparably simple
\begin{lstlisting}
Definition is_LT_mor (A B : LT) (G : LT_mor_data A B) := 
  functor_composite (LT_L A) G = (LT_L B).
\end{lstlisting}
where \lstinline|functor_composite| is the standard composition operator on functors.

With these definitions we get the definition of a Lawvere Theory morphism as
\begin{lstlisting}
Definition LT_mor (A B : LT) := 
  total2 (fun mor_data => is_LT_mor A B mor_data).
\end{lstlisting}

We would like to be guarantee that there are not multiple elements of this type
corresponding to the same functor, which requires us to show that for any
\lstinline|mor_data| that \lstinline|is_LT_mor A B mor_data| is
of h-level 1. The proof for this is
\begin{lstlisting}
Lemma LT_ob_set (A : LT) : isaset (LT_T A).
Proof.
  apply (isofhlevelweqf 2 (X:=nat)).
  - use tpair.
    + exact (LT_L A).
    + exact (pr1 (pr2 A)).
  - apply isasetnat.
Defined.

Lemma is_LT_mor_prop (A B : LT) (G : LT_mor_data A B) : 
  isaprop (is_LT_mor A B G).
Proof.
  assert (isofhlevel 2 (functor F (LT_T B))).
  - apply functor_isaset.
    + exact (homset_property (LT_T B)).
    + apply (LT_ob_set B).
  - unfold isaprop.
    unfold is_LT_mor.
    exact (X (LT_L A · G) (LT_L B)).
Defined.
\end{lstlisting}

First we establish for any Lawvere Theory $(T, L)$ that the objects of $T$ form
a set, which follows from the fact that they are in bijection with a set.
\lstinline|isofhlevelweqf 2| establishes that it is sufficient to show that the
objects of $T$ are in bijection with the natural numbers (which is a given) and
that the natural numbers are of h-level 2 (proven by \lstinline|isasetnat|).

Then, for a specified pair of Lawvere Theories \lstinline|A| and \lstinline|B|
we show that the type of functors from \lstinline|F| to the underlying category
of \lstinline|B| is a set, which follows from the fact that both the objects and
morphisms of the underlying category of \lstinline|B| are sets (using the
theorem \lstinline|functor_isaset|). Since \lstinline|is_LT_mor| is an equality
type between functors from \lstinline|F| to the underlying category of
\lstinline|B|, it must be of h-level 1, as required.

\subsection{Composition and Identities in the Category of Lawvere Theories}
These are expressed simply in terms of functor composition and the identity
functor which are already defined in \textit{UniMath}. A tactic proof style is
used to formalise these, since it allows us to easily give the proof that the
stated functors are valid Lawvere Theory morphisms inline. Note however that
the proofs give the functors themselves as exact terms (without rewriting or
similar) so this part of the resulting term is as simple as expected.
\begin{lstlisting}
Definition LT_cat_identity (A : LT) : LT_mor A A.
Proof.
  use tpair.
  - exact (functor_identity (LT_T A)).
  - cbn.
    unfold is_LT_mor.
    apply functor_identity_right.
Defined.

Definition LT_cat_composition 
  (A B C : LT) (G : LT_mor A B) (H: LT_mor B C) :
  LT_mor A C.
Proof.
  destruct G as [G G_proof].
  destruct H as [H H_proof].
  use tpair.
  - exact (functor_composite G H).
  - cbn.
    unfold is_LT_mor.
    rewrite <- 
      (functor_assoc F (LT_T A) (LT_T B) (LT_T C) (LT_L A) G H).
    rewrite G_proof.
    rewrite H_proof.
    apply idpath.
Defined.
\end{lstlisting}

To show that the identity functor is a Lawvere Theory morphism we need to show
that for a Lawvere Theory $(T, L)$ we have $L\cdot id_T = L$, which is exactly
the right cancellation property of the identity functor. To show that functor
composition of Lawvere Theory morphisms gives a Lawvere Theory morphism we need
to show for Lawvere Theories $(T_A, L_A)$, $(T_B, L_B)$ and $(T_C, L_C)$
and morphisms $G: T_A\to T_B$ and $H: T_B\to T_C$ that $L_A\cdot (G\cdot H) =
L_C$. We rewrite the left hand side with functor associativity to get $(L_A\cdot
G)\cdot H$, then with the morphism property of $G$ to get $L_B\cdot H$ and
finally with the morphism property of $H$ to get $L_C$ as required.

With these definitions of composition and identities we can define the structure
of the Lawvere Theory precategory.
\begin{lstlisting}
Definition LT_cat_data : precategory_data := 
  precategory_data_pair LT_cat_ob_mor LT_cat_identity LT_cat_composition.
\end{lstlisting}

\subsection{Lawvere Theory Precategory}
Given the precategory structure we are required to prove that it obeys the
category axioms to construct a precategory. In this case we do so in a single
proof, using \textit{Coq}'s bullet notation to separate the proofs of the three
axioms.
\begin{lstlisting}
Definition LT_cat_is_precategory : is_precategory LT_cat_data.
Proof.
  repeat split; cbn; intros.
  - unfold LT_cat_composition.
    use total2_paths_f.
    + cbn.
      apply functor_identity_left.
    + apply proofirrelevance.
      apply is_LT_mor_prop.

  - unfold LT_cat_composition.
    use total2_paths_f.
    + cbn.
      apply functor_identity_right.
    + apply proofirrelevance.
      apply is_LT_mor_prop.

  - unfold LT_cat_composition.
    use total2_paths_f.
    + cbn.
      rewrite <- functor_assoc.
      apply idpath.
    + apply proofirrelevance.
      apply is_LT_mor_prop.
Defined.
\end{lstlisting}

Each proof is the construction of an equality type between two particular
Lawvere Theory morphisms. Since a Lawvere Theory morphism is a dependent sum of
a defining functor and a proof that the functor is a valid morphism the proofs
all consist of two parts. Namely proving that defining functors are equal and
proving that the proofs that the functor is a Lawvere Theory morphism are equal.
In all cases the first half of the equality proof follows trivially from
standard theorems about the composition of functors. The second half of each
proof is identical, since for any goal which is an equality between members of
an h-level 1 type we can use the \lstinline|proofirrelevance| lemma, after which
we are only required to prove the type is in fact of h-level 1. In this case
that proof obligation is discharged by the previously proven lemma
\lstinline|is_LT_mor_prop|.

\subsection{Lawvere Theory Category}
The precategory of Lawvere Theories is in fact a category. To prove this we need
to show that the type of morphisms between any two Lawvere Theories is a set, or
equivalently that it has h-level 2.
\begin{lstlisting}
Definition LT_cat_has_homsets (A B : LT) : isaset (LT_mor A B).
Proof.
  change isaset with (isofhlevel 2).
  unfold LT_mor.
  apply isofhleveltotal2.
  + apply (functor_isaset).
    - exact (homset_property (LT_T B)).
    - apply (LT_ob_set B).
  + intros F.
    apply hlevelntosn.
    apply is_LT_mor_prop.
Defined.
\end{lstlisting}
Since a morphism is a pair, we can show that it has h-level 2 by showing that
both its components have h-level 2. That a functor between two Lawvere Theories
has h-level 2 has already been shown and follows from the fact that the
underlying categories of Lawvere Theories must be small categories. It remains
to show that proofs that a functor obeys the morphism property have h-level 2.
However anything of h-level $n$ also has h-level $n+1$, and we have already
shown the type has h-level 1, so has h-level 2 as required.

We can then construct the category as
\begin{lstlisting}
Definition LT_cat : category := category_pair LT_precat LT_cat_has_homsets.
\end{lstlisting}

Unfortunately it is not possible to go further and prove that the category of
Lawvere Theories is small. The collection of Lawvere Theories is a proper class,
which can be proven by observing that the exact value of $L(0)$ for a Lawvere
theory is not actually significant so long as the structure of the category $T$
is correct. Hence, given some Lawvere Theory $A = (T, L)$, for any set $S\not\in
\text{Ob}(T)$ we can construct a Lawvere Theory $A_S$ which is identical to
$A$ except that the object $L(0)$ in $T$ has been replaced by $S$ with the
category structure otherwise identical. Since this gives a unique Lawvere Theory
for every such set, we have an injection from a proper class of sets to the
class of Lawvere Theories, so the class of Lawvere Theories is proper.